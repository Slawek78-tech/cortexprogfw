OUTPUT_FORMAT("elf32-avr","elf32-avr","elf32-avr")
OUTPUT_ARCH(avr:25)

MEMORY
{
  flash  (rx)   : ORIGIN = 0xcc0, LENGTH = 0x1340
  ram    (rw!x) : ORIGIN = 0x8001e0, LENGTH = 0x80
  
  trash	: ORIGIN = 0xffff0000, LENGTH = 0xffff
}

SECTIONS
{
  .text   :
  {
    *(.vecs)
    *(.vecs*)
    KEEP(*(.vecs*))
    KEEP(*(.vecs))
    
    *(.progmem.gcc*)
    *(.progmem*)
    
    . = ALIGN(2);
    *(.jumptables)
    *(.jumptables*)
    *(.lowtext)
    *(.lowtext*)
    
    *(.text)
    *(.text.*)
    
     PROVIDE (__data_data = .) ;
    
  }  > flash

  .data	: AT (ADDR (.text) + SIZEOF (.text))
  {
     PROVIDE (__data_start = .) ;
    *(.data)
    *(.data.*)
    *(.rodata)
    *(.rodata.*)
     PROVIDE (__data_end_bss_start = .) ;
  }  > ram


  .bss :
  {
    *(.bss)
    *(.bss*)
    *(COMMON)
     PROVIDE (__bss_end = .) ;
  }  > ram
  
  .trash :
  {
    *(.vectors)
    *(.vectors.*)
  }  > trash
}


PROVIDE (usbWork = 15 * 2) ;
PROVIDE (packetCanSend = 16 * 2) ;
PROVIDE (packetSend = 17 * 2) ;
PROVIDE (packetRx = 18 * 2) ;
PROVIDE (packetRxRelease = 19 * 2) ;
PROVIDE (bootloader = 20 * 2) ;
PROVIDE (getUsbCaps = 21 * 2) ;
PROVIDE (usbReenumerate = 22 * 2) ;
PROVIDE (blGetBlVersion = 23 * 2) ;